buildscript {
	ext {
		springBootVersion = '1.3.2.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.moowork.gradle:gradle-node-plugin:0.11")
	}
}

plugins {
	id "com.github.kt3k.coveralls" version "2.6.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'
apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

jar {
	baseName = 'potoo'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

task stage {
	dependsOn build
}

npmInstall {
	execOverrides {
		it.workingDir = file("${project.projectDir}/src/webapp")
	}
}

task buildWebApp(type: NpmTask, dependsOn: 'npmInstall') {
	workingDir = file("${project.projectDir}/src/webapp")
	args = ['run', 'build']
}

task copyWebApp(type: Copy, dependsOn: 'buildWebApp') {
	from(file("${project.projectDir}/src/webapp/dist"))
	into(file("${project.buildDir}/resources/main/static"))
}

bootRun.dependsOn copyWebApp
jar.dependsOn copyWebApp

node {
	version = '5.6.0'
	npmVersion = '3.6.0'
	download = true
	workDir = file("${project.projectDir}/src/webapp/nodejs")
	nodeModulesDir = file("${project.projectDir}/src/webapp")
}

task cleanWebApp(type: Delete) {
	delete 'src/webapp/dist', 'src/webapp/node_modules', 'src/webapp/nodejs'
}